class FEBook extends baseFEBook;

#EXEC TEXTURE IMPORT NAME=FEBookTexture1	 FILE=TEXTURES\FE\BookPiece1.bmp GROUP="Icons" FLAGS=1 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEBookTexture2	 FILE=TEXTURES\FE\BookPiece2.bmp GROUP="Icons" FLAGS=1 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEBookTexture3	 FILE=TEXTURES\FE\BookPiece3.bmp GROUP="Icons" FLAGS=1 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEBookTexture4	 FILE=TEXTURES\FE\BookPiece4.bmp GROUP="Icons" FLAGS=1 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEBookTexture5	 FILE=TEXTURES\FE\BookPiece5.bmp GROUP="Icons" FLAGS=1 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEBookTexture6	 FILE=TEXTURES\FE\BookPiece6.bmp GROUP="Icons" FLAGS=1 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=MainTabTexture	 FILE=TEXTURES\FE\MainTab.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FolioTabTexture	 FILE=TEXTURES\FE\FolioTab.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=ReportTabTexture	 FILE=TEXTURES\FE\ReportTab.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=SaveTabTexture	 FILE=TEXTURES\FE\SaveTab.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=ChapterTabTexture	 FILE=TEXTURES\FE\ChapterTab.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=MainTabOverTexture	 FILE=TEXTURES\FE\MainTabOver.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FolioTabOverTexture	 FILE=TEXTURES\FE\FolioTabOver.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=ReportTabOverTexture	 FILE=TEXTURES\FE\ReportTabOver.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=SaveTabOverTexture	 FILE=TEXTURES\FE\SaveTabOver.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=ChapterTabOverTexture	 FILE=TEXTURES\FE\ChapterTabOver.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

//#EXEC TEXTURE IMPORT NAME=FESplashTexture1	 FILE=TEXTURES\FE\MoonTitle1.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
//#EXEC TEXTURE IMPORT NAME=FESplashTexture2	 FILE=TEXTURES\FE\Moontitle2.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
//#EXEC TEXTURE IMPORT NAME=FESplashTexture3	 FILE=TEXTURES\FE\Moontitle3.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
//#EXEC TEXTURE IMPORT NAME=FESplashTexture4	 FILE=TEXTURES\FE\Moontitle4.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
//#EXEC TEXTURE IMPORT NAME=FESplashTexture5	 FILE=TEXTURES\FE\Moontitle5.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
//#EXEC TEXTURE IMPORT NAME=FESplashTexture6	 FILE=TEXTURES\FE\Moontitle6.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FELegalTexture1	 FILE=TEXTURES\FE\Legal1.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELegalTexture2	 FILE=TEXTURES\FE\Legal2.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELegalTexture3	 FILE=TEXTURES\FE\Legal3.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELegalTexture4	 FILE=TEXTURES\FE\Legal4.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELegalTexture5	 FILE=TEXTURES\FE\Legal5.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELegalTexture6	 FILE=TEXTURES\FE\Legal6.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FEEALogoTexture1	 FILE=TEXTURES\FE\EALogo001.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEEALogoTexture2	 FILE=TEXTURES\FE\EALogo002.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEEALogoTexture3	 FILE=TEXTURES\FE\EALogo003.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEEALogoTexture4	 FILE=TEXTURES\FE\EALogo004.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEEALogoTexture5	 FILE=TEXTURES\FE\EALogo005.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEEALogoTexture6	 FILE=TEXTURES\FE\EALogo006.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FELeftReturnUpIcon	 FILE=TEXTURES\Arrows\leftreturnarrowup.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FELeftReturnOverIcon	 FILE=TEXTURES\Arrows\leftreturnover.bmp GROUP="Icons" FLAGS=2 MIPS=OFF


#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture1	 FILE=TEXTURES\FE\ScrollBack1.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture2	 FILE=TEXTURES\FE\ScrollBack2.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture3	 FILE=TEXTURES\FE\ScrollBack3.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture4	 FILE=TEXTURES\FE\ScrollBack4.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture5	 FILE=TEXTURES\FE\ScrollBack5.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEScrollBackTexture6	 FILE=TEXTURES\FE\ScrollBack6.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FEReportBackTexture1	 FILE=TEXTURES\FE\ReportBack1.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEReportBackTexture2	 FILE=TEXTURES\FE\ReportBack2.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEReportBackTexture3	 FILE=TEXTURES\FE\ReportBack3.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEReportBackTexture4	 FILE=TEXTURES\FE\ReportBack4.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEReportBackTexture5	 FILE=TEXTURES\FE\ReportBack5.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEReportBackTexture6	 FILE=TEXTURES\FE\ReportBack6.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FESaveBackTexture1	 FILE=TEXTURES\FE\SAVE\SaveBack1.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FESaveBackTexture2	 FILE=TEXTURES\FE\SAVE\SaveBack2.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FESaveBackTexture3	 FILE=TEXTURES\FE\SAVE\SaveBack3.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FESaveBackTexture4	 FILE=TEXTURES\FE\SAVE\SaveBack4.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FESaveBackTexture5	 FILE=TEXTURES\FE\SAVE\SaveBack5.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FESaveBackTexture6	 FILE=TEXTURES\FE\SAVE\SaveBack6.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture1	 FILE=TEXTURES\Options\001OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture2	 FILE=TEXTURES\Options\002OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture3	 FILE=TEXTURES\Options\003OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture4	 FILE=TEXTURES\Options\004OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture5	 FILE=TEXTURES\Options\005OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEOptionsBackTexture6	 FILE=TEXTURES\Options\006OptionBack.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture1	 FILE=TEXTURES\Folio\FolioBack001.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture2	 FILE=TEXTURES\Folio\FolioBack002.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture3	 FILE=TEXTURES\Folio\FolioBack003.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture4	 FILE=TEXTURES\Folio\FolioBack004.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture5	 FILE=TEXTURES\Folio\FolioBack005.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioBackTexture6	 FILE=TEXTURES\Folio\FolioBack006.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture1	 FILE=TEXTURES\Folio\harryback001.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture2	 FILE=TEXTURES\Folio\harryback002.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture3	 FILE=TEXTURES\Folio\harryback003.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture4	 FILE=TEXTURES\Folio\harryback004.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture5	 FILE=TEXTURES\Folio\harryback005.bmp GROUP="Icons" FLAGS=2 MIPS=OFF
#EXEC TEXTURE IMPORT NAME=FEFolioHarryTexture6	 FILE=TEXTURES\Folio\harryback006.bmp GROUP="Icons" FLAGS=2 MIPS=OFF

var UWindowButton MainTab;
var UWindowButton ReportTab;
var UWindowButton FolioTab;
var UWindowButton ChapterTab;
var UWindowButton SaveTab;
var UWindowButton BtnGotoStoryBook;

var UWindowButton BackPageButton;

var UWindowSmallButton DismissButton;


var hpMessageBox ConfirmQuitGame;

var baseFEPage	MainPage;
var baseFEPage	ReportPage;
var baseFEPage	FolioPage;
var baseFEPage	ChapterPage;
var baseFEPage	SavePage;

var baseFEPage	SlotPage;

var baseFEPage  StoryBookPage;
// AMM
var baseFEPage	CreditsPage;

var baseFEPage	LevSelectPage;
var baseFEPage	LoadPage;

var baseFEPage	OptionsPage;

var baseFEPage	QuidMatchPage;
var baseFEPage	SplashPage;

var baseFEPage	LangPage;

var baseFEPage	curPage;
var baseFEPage	prevPage;

var string      _URLToLoad;
var bool        bTravelItemsOnLoad;
//var float       _URLToLoadTimer;
var bool bShowTabs;

var Texture LogoTexture1,LogoTexture2;

var bool bIsOpen;
var bool bShowBackground;

var bool bDrawLogo;

// AMM
var bool bInEndGame;

struct Background
{
	var Texture p1,p2,p3,p4,p5,p6;
	var float durration;
};

var Background SplashScreens[5];
var int numSplashScreens;
var int curSplashScreen;

var bool bShowSplash;
var float fShowSplashTime;

var bool bNewGame;			// Level was loaded because New Game button was clicked
var bool bGamePlaying;

//When you push esc in the story book, you need to go to the "menu".  An esc here needs to send you back to
// the story book.  I say time for a hack...  Who's in?
var bool bCameFromStoryBook;

//var UWindowSmallButton VersionButton;

var UWindowWrappedTextArea StatusBarTextWindow;

var bool bResolutionChanged;

var Background BookBackground;
var Background CloudBackground;
var Background ScrollBackground;
var Background ReportBackground;
var Background SaveBackground;
var Background OptionsBackground;
var Background FolioBackground;
var Background FolioHarryBackground;

var Background curBackground;

var bool bPlayingQuidditch;


function SaveSelectedSlot()
{
	if(SlotPage!=None)
		{
		FESlotPage(SlotPage).SaveSelectedSlot();
		}
}
function LoadSelectedSlot()
{
	if(SlotPage!=None)
		{
		FESlotPage(SlotPage).LoadSelectedSlot();
		}
}

function ToolTip(string strTip)
{

//	StatusBarText = strTip;
	StatusBarTextWindow.clear();

	StatusBarTextWindow.addText(strTip);

}
function ResolutionChanged(float W, float H)
{
	Super.ResolutionChanged(W,H);
	bResolutionChanged=true;
}

function string GetLocalizedString(string id,optional out string tip)
{
//	if(tip!=None)
//		{
		tip=Localize( "help", id,"HPMenu");
//		}
	return(Localize( "text", id,"HPMenu" ));	//resume game. utb	"Return to Game";

}

function ChangePage(baseFEPage page)
{
	log("ChangePage"@ page);

	if(curPage!=page)
	{
		prevPage=curPage;

		if (curPage != None)
			curPage.HideWindow();

		curPage=page;
		if (curPage != None)
		{
			curPage.PreSwitchPage();
			curPage.ShowWindow();
		}
			//Page transition sound
//		baseHarry(Root.Console.viewport.actor).PlaySound(Sound'HPSounds.Magic_sfx.pickups.beans_good');	//beancounter pickup_page
	}

	if(bGamePlaying)
		GetLocalizedString("report_buttons_03",BackPageButton.ToolTipString); //resume game. utb	"Return to Game";
	else
		GetLocalizedString("main_menu_01",BackPageButton.ToolTipString); // main menu. utb "Return to Main Menu";


	bShowBackground=true;
		//decide what tabs to show with this page.
	switch(curPage)
		{
		case SplashPage:
			//setup the first splash screen
			bShowSplash=true;
			curSplashScreen=0;
			fShowSplashTime=SplashScreens[curSplashScreen].durration;

			ShowBackButton(false);
			ShowTabs(false);
			break;
		case MainPage:
/*			if(bGamePlaying)
				{
				ShowBackButton(true);
				}
			else
				{*/

				bPlayingQuidditch = False;
				bCameFromStoryBook = False;
				bGamePlaying = False;
				// AMM
				bInEndGame = False;
				ShowBackButton(false);
//				}
			ShowTabs(false);

			baseHarry(Root.Console.viewport.actor).ClientSetMusic( Music'Engine.JS_HP_Title_Screen_v2', 0, 255, MTRAN_Instant, false );

			curBackground=CloudBackground;
			break;
		case ReportPage:
			ShowBackButton(true);
			ShowTabs(true);
			curBackground=ReportBackground;
			break;
		case FolioPage:
			ShowBackButton(true);
			ShowTabs(true);
			//curBackground=FolioBackground; // set by the folio itself 

				//cmp 10-16 bug fix. eur2993
			GetLocalizedString("back_button",BackPageButton.ToolTipString);

			break;
		case ChapterPage:
			ShowBackButton(true);
			ShowTabs(true);
			curBackground=BookBackground;
			break;
		case SavePage:
			ShowBackButton(true);
			ShowTabs(true);
			curBackground=BookBackground;
			break;

		case SlotPage:
			ShowBackButton(true);
			ShowTabs(false);
			curBackground=SaveBackground;
			break;

		case StoryBookPage:
			ShowBackButton(false);
			ShowTabs(false);
			curBackground=BookBackground;
			break;
		// AMM
		case CreditsPage:
			ShowBackButton(false);
			ShowTabs(false);
			break;

		case LevSelectPage:
			ShowBackButton(true);
			ShowTabs(false);
			curBackground=BookBackground;
			break;
		case LoadPage:
			ShowBackButton(true);
			ShowTabs(bGamePlaying);		//show tabs if still in a game.
			curBackground=BookBackground;
			break;

		case OptionsPage:
			ShowBackButton(true);
			if(bGamePlaying)
				ShowTabs(true);
			else
				ShowTabs(false);
			curBackground=OptionsBackground;
				//cmp 10-16 bug fix. eur2993
			GetLocalizedString("back_button",BackPageButton.ToolTipString);
			break;

		case QuidMatchPage:
			ShowBackButton(false);
			ShowTabs(false);
			break;
		case LangPage:
			ShowBackButton(false);
			ShowTabs(false);
			bShowBackground=false;
			break;
		}

		
}

function ChangePagePrevious()
{
	if(prevPage!=None && prevPage!=curPage)
		{
		changePage(prevPage);
		prevPage=None;
		}
}

function ChangePageNamed(string name)
{
	
	switch(caps(name))
		{
		case "SPLASH":
			changePage(SplashPage);
			break;
		case "SLOT":
			changePage(SlotPage);
			break;
		case "MAIN":
			changePage(MainPage);
			break;
		case "LEVSELECT":
			changePage(LevSelectPage);
			break;
		case "LOAD":
			changePage(LoadPage);
			break;
		case "STORYBOOKPAGE":
			changePage(StoryBookPage);
			break;
	
		// AMM
		case "CREDITSPAGE":
			changePage(CreditsPage);
			break;
		case "ENDGAME":
			EndGame();
			break;
		// AMM - END 
			
		case "FOLIO":
			changePage(FolioPage);
			break;

		case "OPTIONS":
			changePage(OptionsPage);
			break;

		case "REPORT":
			changePage(ReportPage);
			break;

		case "CHAPTER":
			changePage(ChapterPage);
			break;

		case "QUIDMATCH":
			changePage(QuidMatchPage);
			break;
		case "LANG":
		case "LANGUAGE":
			changePage(LangPage);
			break;
		}
}
function ShowBackButton(bool flag)
{
	if(flag)
		{
		BackPageButton.ShowWindow();	
		}
	else
		{
		BackPageButton.HideWindow();	
		}
}

function ShowTabs(bool flag)
{
return;
flag=false;

	bShowTabs=flag;
	if(flag)
		{
		MainTab.ShowWindow();	
		ReportTab.ShowWindow();	
		FolioTab.ShowWindow();	
//		ChapterTab.ShowWindow();
//		SaveTab.ShowWindow();	
		}
	else
		{
		MainTab.HideWindow();	
		ReportTab.HideWindow();	
		FolioTab.HideWindow();	
		ChapterTab.HideWindow();
		SaveTab.HideWindow();	
		}

//ReportTab.ShowWindow();	

}

//****************************************************************************************
// Set _URLToLoad to the map you want to load, and tick'll pick it up.
//  This is all that RunURL( string levURL ) does.
event Tick(float delta)
{
	local bool	bTravelItems;

	if( _URLToLoad != "" )
	{
		bCameFromStoryBook = false;  //I'll set this here for safety.

		// If we're passing items, assume we're in the normal hub-to-hub game flow
		HPConsole(root.console).bInHubFlow = bTravelItemsOnLoad;

		// Suppress passing items if this is the first level of a new game
		if ( bNewGame )
		{
			bTravelItems = false;
			bNewGame = false;
		}
		else
			bTravelItems = bTravelItemsOnLoad;

		// Switch levels
		baseConsole(root.console).ChangeLevel(_URLToLoad,bTravelItems);
//		root.console.Viewport.Actor.Level.ServerTravel( _URLToLoad, bTravelItems );
		_URLToLoad = "";

		CloseBook();
	}

	if(bShowSplash)
		{
		fShowSplashTime-=delta;
		}

}


function Created()
{
local int i;
local Texture tempTexture;

	Super.Created();
 
	bNewGame = false;

/*	tempTexture=Texture'MainTabTexture';
	MainTab = UWindowButton(CreateControl(class'UWindowButton',  
		575,40,tempTexture.USize, tempTexture.VSize));
	MainTab.ToolTipString="Main Menu";
	MainTab.UpTexture=Texture'MainTabTexture';
	MainTab.DownTexture=Texture'MainTabOverTexture';
	MainTab.OverTexture=Texture'MainTabOverTexture';
	
	ReportTab = UWindowButton(CreateControl(class'UWindowButton',  
		577,100,tempTexture.USize, tempTexture.VSize));
	ReportTab.ToolTipString="Report Card";
	ReportTab.SetText("Report Card");
	ReportTab.UpTexture=Texture'ReportTabTexture';
	ReportTab.DownTexture=Texture'ReportTabOverTexture';
	ReportTab.OverTexture=Texture'ReportTabOverTexture';


	FolioTab = UWindowButton(CreateControl(class'UWindowButton',  
		579,160,tempTexture.USize, tempTexture.VSize));
	FolioTab.ToolTipString="Wizard Cards";
	FolioTab.UpTexture=Texture'FolioTabTexture';
	FolioTab.DownTexture=Texture'FolioTabOverTexture';
	FolioTab.OverTexture=Texture'FolioTabOverTexture';

	ChapterTab = UWindowButton(CreateControl(class'UWindowButton',  
		581,220,tempTexture.USize, tempTexture.VSize));
	ChapterTab.ToolTipString="Storybooks";
	ChapterTab.UpTexture=Texture'ChapterTabTexture';
	ChapterTab.DownTexture=Texture'ChapterTabOverTexture';
	ChapterTab.OverTexture=Texture'ChapterTabOverTexture';

	SaveTab = UWindowButton(CreateControl(class'UWindowButton',  
		581,220,tempTexture.USize, tempTexture.VSize));
	SaveTab.ToolTipString="Save Game";
	SaveTab.UpTexture=Texture'SaveTabTexture';
	SaveTab.DownTexture=Texture'SaveTabOverTexture';
	SaveTab.OverTexture=Texture'SaveTabOverTexture';

	ShowTabs(false);	//start hidden
*/
	tempTexture=Texture'FELeftReturnUpIcon';
	BackPageButton = UWindowButton(CreateControl(class'UWindowButton',  
		(WinWidth-75)+0,(WinHeight-39)-10,75, 39));
	BackPageButton.UpTexture=Texture'FELeftReturnUpIcon';
	BackPageButton.DownTexture=Texture'FELeftReturnOverIcon';
	BackPageButton.OverTexture=Texture'FELeftReturnOverIcon';
	BackPageButton.Register(self);
	BackPageButton.hideWindow();


		//BtnGotoStoryBook = UWindowButton(CreateControl(class'UWindowButton',  
		//	560,360,tempTexture.USize, tempTexture.VSize));
		//BtnGotoStoryBook.ToolTipString="Report Page";
		//BtnGotoStoryBook.UpTexture=Texture'SaveTabTexture';
		//BtnGotoStoryBook.DownTexture=Texture'SaveTabOverTexture';
		//BtnGotoStoryBook.OverTexture=Texture'SaveTabOverTexture';

	MainPage = baseFEPage(CreateWindow(class'FEMainPage',0,0,640,480)); 
	MainPage.book=self; 
	MainPage.hideWindow();

	SlotPage = baseFEPage(CreateWindow(class'FESlotPage',0,0,640,480)); 
	SlotPage.book=self; 
	SlotPage.hideWindow();


	ReportPage = baseFEPage(CreateWindow(class'FEReportPage',0,0,640-55,480-40));  
	ReportPage.book=self; 
	ReportPage.hideWindow();

	FolioPage = baseFEPage(CreateWindow(class'FEFolioPage',0,0,640-60,480-30));  
	FolioPage.book=self; 
	FolioPage.hideWindow();

	ChapterPage = baseFEPage(CreateWindow(class'FEChapterPage',45,10,528,416));  
	ChapterPage.book=self; 
	ChapterPage.hideWindow();

	SavePage = baseFEPage(CreateWindow(class'FESavePage',45,10,528,416));  
	SavePage.book=self; 
	SavePage.hideWindow();

	LevSelectPage = baseFEPage(CreateWindow(class'FELevSelectPage',45,10,528,416));  
	LevSelectPage.book=self; 
	LevSelectPage.hideWindow();

	OptionsPage = baseFEPage(CreateWindow(class'FEOptionsPage',45,10,528,430)); 
	OptionsPage.book=self; 
	OptionsPage.hideWindow();

	LoadPage = baseFEPage(CreateWindow(class'FELoadPage',45,10,528,416));  
	LoadPage.book=self; 
	LoadPage.hideWindow();

	//Full screen
	StoryBookPage = baseFEPage(CreateWindow(class'FEStoryBookPage',0,0,640,480));
	StoryBookPage.book=self; 
	StoryBookPage.hideWindow();

	//Full screen
	QuidMatchPage = baseFEPage(CreateWindow(class'FEQuidMatchPage',0,0,640,480)); 
	QuidMatchPage.book=self; 
	QuidMatchPage.hideWindow();

	LangPage = baseFEPage(CreateWindow(class'FESoundBrowser',0,0,640,480));  
	LangPage.book=self; 
	LangPage.hideWindow();


		//debug mode button to allow you to go into a level specified on the command line
	DismissButton = UWindowSmallButton(CreateControl(class'UWindowSmallButton',WinWidth-10,0,10, 10));
	DismissButton.setFont(F_HPMenuLarge);




	StatusBarTextWindow = UWindowWrappedTextArea( CreateControl(class'UWindowWrappedTextArea', 0, WinHeight-26, 500, 26) );
	StatusBarTextWindow.Clear();
	StatusBarTextWindow.AddText("");
	StatusBarTextWindow.Font = F_HPMenuLarge;

	bShowBackground=true;

	CloudBackground.p1=Texture( DynamicLoadObject("MenuArt.MoonTitle1", class'Texture') );
	CloudBackground.p2=Texture( DynamicLoadObject("MenuArt.MoonTitle2", class'Texture') );
	CloudBackground.p3=Texture( DynamicLoadObject("MenuArt.MoonTitle3", class'Texture') );
	CloudBackground.p4=Texture( DynamicLoadObject("MenuArt.MoonTitle4", class'Texture') );
	CloudBackground.p5=Texture( DynamicLoadObject("MenuArt.MoonTitle5", class'Texture') );
	CloudBackground.p6=Texture( DynamicLoadObject("MenuArt.MoonTitle6", class'Texture') );
	CloudBackground.durration=999999.0;

	if(HPConsole(root.console).bUseAsianFont || HPConsole(root.console).bUseThaiFont)
		{
		bDrawLogo=false;
		}
	else
		{
		LogoTexture1 = Texture( DynamicLoadObject("MenuArt.Logo1", class'Texture') );
		LogoTexture1.bMasked=true;
		LogoTexture2 = Texture( DynamicLoadObject("MenuArt.Logo2", class'Texture') );
		LogoTexture2.bMasked=true;
		bDrawLogo=true;
		}
	CurPage=None;

/*
local LevelInfo lev;

			//find out if this was an autoplay level
	lev=GetLevel();
	if( InStr(caps(lev.GetLocalUrl()),"AUTOPLAY")>=0)
		{	//yup so bypass the menus
		bGamePlaying=true;
		bShowSplash=false;
		CloseBook();
		}

*/
}

function ScaleAndDraw(Canvas canvas,float x,float y,Texture tex)
{
local float fx,fy;

	if(tex==None)
		return;

	fx=(canvas.SizeX/640.0);
	fy=(canvas.SizeY/480.0);
fx=1;fy=1;

	DrawStretchedTexture( canvas, x*fx, y*fy, tex.USize*fx,tex.VSize*fy,tex);

}

function handleSplash()
{
	if(!bShowSplash)
		return;

	root.DisableMouse();

	if(fShowSplashTime<0)
		{
		curSplashScreen++;
		if(curSplashScreen>=numSplashScreens)
			{
			ChangePageNamed("Main");
			bShowSplash=false;
			root.EnableMouse();
			}
		fShowSplashTime=SplashScreens[curSplashScreen].durration;
		}


} 

function Paint(Canvas canvas,float x,float y)
{
local int width;
local int i;
local int ox,oy;
local color saveColor;

	if(bResolutionChanged)
		{
		root.SetScale(root.realwidth/640);
		bResolutionChanged=false;
		}

	if(bShowSplash)
		{
			//check for splash change
		handleSplash();

		ScaleAndDraw(canvas,0,0,SplashScreens[curSplashScreen].p1);
		ScaleAndDraw(canvas,256,0,SplashScreens[curSplashScreen].p2);
		ScaleAndDraw(canvas,512,0,SplashScreens[curSplashScreen].p3);

		ScaleAndDraw(canvas,0,256,SplashScreens[curSplashScreen].p4);
		ScaleAndDraw(canvas,256,256,SplashScreens[curSplashScreen].p5);
		ScaleAndDraw(canvas,512,256,SplashScreens[curSplashScreen].p6);
		}
	else
		{

		if(bShowBackground)
			{
			ScaleAndDraw(canvas,0,0,curBackground.p1);
			ScaleAndDraw(canvas,256,0,curBackground.p2);
			ScaleAndDraw(canvas,512,0,curBackground.p3);

			ScaleAndDraw(canvas,0,256,curBackground.p4);
			ScaleAndDraw(canvas,256,256,curBackground.p5);
			ScaleAndDraw(canvas,512,256,curBackground.p6);
			if(CurBackground==CloudBackground && bDrawLogo)
				{
				ScaleAndDraw(canvas,74,243,logoTexture1);
				ScaleAndDraw(canvas,330,243,logoTexture2);
				}

			}
		else
			{
			DrawStretchedTextureSegment( canvas , 0,0,640,480,0,0,0,0,curBackground.p1); 
			}
		if(curPage!=None)
			if(!curPage.bWindowVisible)
				curPage.ShowWindow();
		}

	if(HPConsole(root.console).bDebugMode)
		{
		canvas.SetPos(0,0);
		canvas.DrawText("Debug");
		}


}

//****************************************************************************************
// This right now is also called from FEStoryBookPage, when a "storybook interlude" has finished.
//function GoBackToGame()		//replaced by CloseBook();
//{
//
//}

//****************************************************************************************

function OpenBook(optional string pageName)
{
	// Take a snapshot of the screen, for a possible save game:
	HPConsole(root.console).viewport.Actor.ConsoleCommand("Snap 3");

		//revisit: find out what bLocked and quickkeyenable are.
	if (HPConsole(root.console).bLocked)
		return;
	HPConsole(root.console).bQuickKeyEnable = False;

	HPConsole(root.console).LaunchUWindow();

	bIsOpen=true;
	if (pageName != "")
		ChangePageNamed(pageName);

	log("OpenBook"@ pageName $","@ CurPage);

	if(CurPage!=None)
		CurPage.PreOpenBook();

}
function CloseBook()
{
	//Allright, now the StoryBook special case.
	//If bCameFromStoryBook is true, just go back to the StoryBook
	//Log("*********** CloseBook bCameFromStoryBook="$bCameFromStoryBook);
	if( bCameFromStoryBook )
	{
		OpenBook( "STORYBOOKPAGE" );
	}
	else
	{
		root.console.CloseUWindow();
		bIsOpen=false;
	}
}
function WindowDone(UWindowWindow W)
{
	if(W == ConfirmQuitGame)
	{
		if(ConfirmQuitGame.Result == ConfirmQuitGame.button1.text)
		{
			bGamePlaying=false;
			bCameFromStoryBook = false;
			ChangePage(MainPage);
		}
		else
		{
			if( bPlayingQuidditch )
				CloseBook();
		}
		ConfirmQuitGame = None;
	}
}


function HPMessageBox doHPMessageBox(string msg, string textButton1, optional string textButton2, optional float timeOut)
{
	local HPMessageBox w;
	
	w = HPMessageBox(Root.CreateWindow(class'HPMessageBox', (640-246)/2, (480-102)/2, 246, 102, Self));
	w.Setup (msg, textButton1, textButton2, timeOut);

	root.ShowModal(w);

	return w;
}


//***********************************************************************************************
function EscFromConsole()
{
	if( bPlayingQuidditch )
	{
		//revisit: find out what bLocked and quickkeyenable are.
		if (HPConsole(root.console).bLocked)
			return;
		HPConsole(root.console).bQuickKeyEnable = False;

		HPConsole(root.console).LaunchUWindow();

		log("Quidditch Escape!!!");
		ConfirmQuitGame = doHPMessageBox(
			GetLocalizedString("report_buttons_05"), //"Are you sure you want to quit this game?"
			GetLocalizedString("main_menu_09"),// "Yes"
			GetLocalizedString("main_menu_10") //"No"
			);
	}
	else
	{
		OpenBook("REPORT");
	}
}



function Notify(UWindowDialogControl C, byte E)
{

	if(e==DE_Click)
		{
		switch(c)
			{
			case MainTab:
				if(bGamePlaying)
					{
/*					ConfirmQuitGame = doHPMessageBox(
						GetLocalizedString("report_buttons_05"), //"Are you sure you want to quit this game?"
						GetLocalizedString("main_menu_09"),// "Yes"
						GetLocalizedString("main_menu_10") //"No"
						);*/
					}
				else
					ChangePage(MainPage);
				break;
/*			case ReportTab:
				ChangePage(ReportPage);
				break;
			case FolioTab:
				ChangePage(FolioPage);
				break;
			case ChapterTab:
				ChangePage(ChapterPage);
				break;
			case SaveTab:
				ChangePage(SavePage);
				break;
*/
			case BtnGotoStoryBook:
				ChangePage(StoryBookPage);
				break;
			case BackPageButton:
				if(bGamePlaying 
					&& curPage==ReportPage
					)
					CloseBook();
				else
					ChangePagePrevious();
				break;
			case DismissButton:
				CloseBook();
				break;
			}
		}
}

function WindowEvent(WinMessage Msg, Canvas C, float X, float Y, int Key) 
{
	if(Msg == WM_Paint || !root.WaitModal())
		Super.WindowEvent(Msg, C, X, Y, Key);
}



//***********************************************************************************************
function bool KeyEvent( byte/*EInputKey*/ Key, byte/*EInputAction*/ Action, FLOAT Delta )
{
	// Dont process key if root window is displaying a modal dialog
	if( root.ModalWindow != None )
		return false;

	//check early dismiss of splash screen.
	if(bShowSplash &&HPConsole(root.console).bDebugMode)
		if(Action==1 /*IST_Press*/)
			{
			fShowSplashTime=-1;	//force change of splash screen.
			return true;
			}

	// AMM 
	// If page handles key then early out
	if(curPage != None )
	{
		if ( curPage.KeyEvent( Key, Action, Delta ) )
		{
			return true;
		}
	}

	// This could easily be done in FEFolio page, but doing it here localises changes to just this file
	if( bInEndGame && curPage == FolioPage )
	{
		if(Action==1 && key==0x1b )	// Escape takes moves on to credits if in end game
		{
			// FEBook(book).bGamePlaying = false;
			ChangePageNamed("CREDITSPAGE");

			return true;
		}
	}
	// AMM - End

	//handle escape key
	if(Action==1 /*IST_Press*/ && key==0x1b /*escape*/	&&	curPage!=StoryBookPage)	//storybook handles its own escape.
		{
		//Escape always takes you back to game if playing.
		//Actually, it doesn't now.  If bCameFromStoryBook is true, it will just go to the StoryBook Page.
		if(   (bGamePlaying || bCameFromStoryBook)
		   && curPage != MainPage
		  )
			{
			CloseBook();
			return(true);
			}
		else
			{
				//else it takes you back one screen.
			if(curPage!=MainPage)
				{
				ChangePagePrevious();
				return(true);
				}
			else
				return(true);	//escape on main page does nothing for now.
			}
			
		}

	// AMM - Remove this shit
//	if(CurPage!=None)
//		return curPage.KeyEvent( Key, Action, Delta );
//	else
//		return(false);

	return false;
}

//***********************************************************************************************
//Called from FELevSelectPage, and FEStoryBookPage
function RunURL( string levURL, bool bTravelItems )
{
	bTravelItemsOnLoad = bTravelItems;
	_URLToLoad = levURL;

	bGamePlaying=true;
	bCameFromStoryBook = false;
}

//***********************************************************************************************
// AMM
function EndGame()
{
	local baseHarry harry;

	// This changes behaviour of space and escape for folio page
	bInEndGame = true;

	if( CreditsPage == None )
	{
		CreditsPage = baseFEPage(CreateWindow(class'FECreditsPage',0,0,640,480));
		CreditsPage.book=self; 
		CreditsPage.hideWindow();
	}

	FEFolioPage(FolioPage).PageNum=6;

	harry = baseHarry(Root.Console.viewport.actor);

	if (harry.getNumCards() == ArrayCount(harry.WizardCards))
//	if (true)
	{
//		harry.addcard (100);
		FEFolioPage(FolioPage).selectedCardID=100;
	}

	changePage(FolioPage);

	ShowBackButton(false);
}
function ShowCredits()
{
	local baseHarry harry;

	// This changes behaviour of space and escape for folio page
	bInEndGame = true;

	if( CreditsPage == None )
	{
		CreditsPage = baseFEPage(CreateWindow(class'FECreditsPage',0,0,640,480));
		CreditsPage.book=self; 
		CreditsPage.hideWindow();
	}

	ShowBackButton(false);
	ChangePageNamed("CREDITSPAGE");
}


//***********************************************************************************************
//Called from FELevSelectPage, and FEStoryBookPage
function DoStoryBookInterlude( int StoryBookIdx, name EventWhenDone )
{
	// AMM
	if ( StoryBookIdx == 16 )
	{
		OpenBook( "ENDGAME" );
	}
	else
	{
		FEStoryBookPage(StoryBookPage).SetStory( StoryBookIdx, "", false/*dont go back to chapter page when done*/, EventWhenDone );
		OpenBook( "STORYBOOKPAGE" );
	}
}

//***********************************************************************************************

defaultproperties
{
     SplashScreens(0)=(p1=Texture'HPMenu.Icons.FEEALogoTexture1',p2=Texture'HPMenu.Icons.FEEALogoTexture2',p3=Texture'HPMenu.Icons.FEEALogoTexture3',p4=Texture'HPMenu.Icons.FEEALogoTexture4',p5=Texture'HPMenu.Icons.FEEALogoTexture5',p6=Texture'HPMenu.Icons.FEEALogoTexture6',durration=12)
     SplashScreens(1)=(p1=Texture'HPMenu.Icons.FELegalTexture1',p2=Texture'HPMenu.Icons.FELegalTexture2',p3=Texture'HPMenu.Icons.FELegalTexture3',p4=Texture'HPMenu.Icons.FELegalTexture4',p5=Texture'HPMenu.Icons.FELegalTexture5',p6=Texture'HPMenu.Icons.FELegalTexture6',durration=12)
     numSplashScreens=2
     BookBackground=(p1=Texture'HPMenu.Icons.FEBookTexture1',p2=Texture'HPMenu.Icons.FEBookTexture2',p3=Texture'HPMenu.Icons.FEBookTexture3',p4=Texture'HPMenu.Icons.FEBookTexture4',p5=Texture'HPMenu.Icons.FEBookTexture5',p6=Texture'HPMenu.Icons.FEBookTexture6',durration=999999)
     CloudBackground=(durration=999999)
     ScrollBackground=(p1=Texture'HPMenu.Icons.FEScrollBackTexture1',p2=Texture'HPMenu.Icons.FEScrollBackTexture2',p3=Texture'HPMenu.Icons.FEScrollBackTexture3',p4=Texture'HPMenu.Icons.FEScrollBackTexture4',p5=Texture'HPMenu.Icons.FEScrollBackTexture5',p6=Texture'HPMenu.Icons.FEScrollBackTexture6',durration=999999)
     ReportBackground=(p1=Texture'HPMenu.Icons.FEReportBackTexture1',p2=Texture'HPMenu.Icons.FEReportBackTexture2',p3=Texture'HPMenu.Icons.FEReportBackTexture3',p4=Texture'HPMenu.Icons.FEReportBackTexture4',p5=Texture'HPMenu.Icons.FEReportBackTexture5',p6=Texture'HPMenu.Icons.FEReportBackTexture6',durration=999999)
     SaveBackground=(p1=Texture'HPMenu.Icons.FESaveBackTexture1',p2=Texture'HPMenu.Icons.FESaveBackTexture2',p3=Texture'HPMenu.Icons.FESaveBackTexture3',p4=Texture'HPMenu.Icons.FESaveBackTexture4',p5=Texture'HPMenu.Icons.FESaveBackTexture5',p6=Texture'HPMenu.Icons.FESaveBackTexture6',durration=999999)
     OptionsBackground=(p1=Texture'HPMenu.Icons.FEOptionsBackTexture1',p2=Texture'HPMenu.Icons.FEOptionsBackTexture2',p3=Texture'HPMenu.Icons.FEOptionsBackTexture3',p4=Texture'HPMenu.Icons.FEOptionsBackTexture4',p5=Texture'HPMenu.Icons.FEOptionsBackTexture5',p6=Texture'HPMenu.Icons.FEOptionsBackTexture6',durration=999999)
     FolioBackground=(p1=Texture'HPMenu.Icons.FEFolioBackTexture1',p2=Texture'HPMenu.Icons.FEFolioBackTexture2',p3=Texture'HPMenu.Icons.FEFolioBackTexture3',p4=Texture'HPMenu.Icons.FEFolioBackTexture4',p5=Texture'HPMenu.Icons.FEFolioBackTexture5',p6=Texture'HPMenu.Icons.FEFolioBackTexture6',durration=999999)
     FolioHarryBackground=(p1=Texture'HPMenu.Icons.FEFolioHarryTexture1',p2=Texture'HPMenu.Icons.FEFolioHarryTexture2',p3=Texture'HPMenu.Icons.FEFolioHarryTexture3',p4=Texture'HPMenu.Icons.FEFolioHarryTexture4',p5=Texture'HPMenu.Icons.FEFolioHarryTexture5',p6=Texture'HPMenu.Icons.FEFolioHarryTexture6',durration=999999)
}
